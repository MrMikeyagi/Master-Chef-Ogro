
// implements some editing commands


//////// Entity Editing ///////////////

// the entity type of current selection
et = [ at (entget) 0 ]

// access the given attribute of selected ent
ea = [ at (entget) (+ $arg1 1) ]

// clear ents of given type
clearents = [ 
  if $editing [
    entcancel
    entselect [ strcmp (et) $arg1 ]; 
    echo Deleted (enthavesel) $arg1 entities; 
    delent
  ] 
]

// replace all ents that match current selection
// with the values given
replaceents = [
  if $editing [
    entfind @(entget)
    entset @arg1 @arg2 @arg3 @arg4 @arg5
    echo Replaced (enthavesel) entities  
  ]
]

selentedit      = [ "saycommand" ( concatword "/entset " (entget) ) ] 
selreplaceents  = [ "saycommand" ( concatword "/replaceents " (entget) ) ]  
selentfindall   = [ do [ @( concatword "entfind " (entget) ) ] ]

// modify given attribute of ent by a given amount
// arg1 attribute
// arg2 value
entproperty = [
  entloop [
    a0 = (ea 0)
    a1 = (ea 1)
    a2 = (ea 2)
    a3 = (ea 3)
    a4 = (ea 4)
    do [a@arg1 = (+ (ea @arg1) @arg2)]
    entset (et) $a0 $a1 $a2 $a3 $a4
  ]
]

unisersalents = [light mapmodel playerstart envmap particles sound spotlight]

enttypelistfps = [ 
  light mapmodel playerstart envmap particles sound spotlight
  teleport teledest carrot jumppad banana respawnpoint
  creature box barrel platform elevator dynlight
]

enttypelistrpg = [ 
  light mapmodel playerstart envmap particles sound spotlight spawn teleport teledest jumppad checkpoint
]

enttypelistmovie = [
  light mapmodel playerstart envmap particles sound spotlight
  box barrel platform elevator actor script waypoint camera
]

enttypelistssp = [
	light mapmodel playerstart envmap particles sound spotlight
	box pickup enemy waypoint teleport teledest checkpoint jumppad
	platform paltformroute camera axis
]

enttypelength = ( listlen (getalias (concatword "enttypelist" (getgame))) )

listindex = [
  s = 0
  n = (listlen $arg1)
  loop i $n [
    if (strcmp $arg2 (at $arg1 $i)) [
      s = $i
    ]
  ]
  result $s
]

enttypeselect = [     
  n = $enttypelength
  next = (+ (listindex $enttypelist@(getgame) (et)) $arg1)
  if (< $next 0)  [ next = (- $n 1) ]
  if (= $next $n) [ next = 0 ]
  next = (at $enttypelist@(getgame) $next)
  entset $next (ea 0) (ea 1) (ea 2) (ea 3) (ea 4)
]

////// Entity primary actions /////////

ent_action_banana         = [ entproperty 0 ( * $arg1 1 ) ]
ent_action_carrot         = [ entproperty 0 ( * $arg1 1 ) ]
ent_action_teleport       = [ entproperty 0 ( * $arg1 1 ) ]
ent_action_teledest       = [ entproperty 1 ( * $arg1 1 ) ]
ent_action_mapmodel       = [ entproperty 1 ( * $arg1 1 ) ]
ent_action_spotlight      = [ entproperty 0 ( * $arg1 5 ) ]
ent_action_light          = [ entproperty 0 ( * $arg1 5 ) ]
ent_action_dynlight       = [ @ent_action_light ]
ent_action_jumppad        = [ entproperty 0 ( * $arg1 5 ) ]
ent_action_respawnpoint   = [ entproperty 0 ( * $arg1 15 ) ]
ent_action_playerstart    = [ entproperty 0 ( * $arg1 15 ) ]
ent_action_envmap         = [ entproperty 0 ( * $arg1 5 ) ]
ent_action_particles      = [ entproperty 0 ( * $arg1 1 ) ]
ent_action_sound          = [ entproperty 0 ( * $arg1 1 ) ]
ent_action_box            = [ entproperty 1 ( * $arg1 1 ) ]
ent_action_barrel         = [ entproperty 1 ( * $arg1 1 ) ]
ent_action_platform       = [ entproperty 1 ( * $arg1 1 ) ]
ent_action_elevator       = [ entproperty 1 ( * $arg1 1 ) ]
ent_action_creature       = [ entproperty 1 ( * $arg1 1 ) ]

//////// Copy and Paste //////////////

// 3 types of copying and pasting
// 1. select only cubes      -> paste only cubes
// 2. select cubes and ents  -> paste cubes and ents. same relative positions
// 3. select only ents       -> paste last selected ent. if ents are selected, replace attrs as paste

opaquepaste = 1
entcopybuf = ""

entreplace   = [ 
  do [
    if (enthavesel) [] [ newent @entcopybuf ]
    entset @entcopybuf 
  ]
]

editcopy   = [
  if (|| havesel [! (enthavesel)]) [
    entcopybuf = ""
    entcopy
    copy
  ] [
    entcopybuf = (entget)
  ]
]

editpaste  = [ 
  cancelpaste = (! (|| enthavesel havesel));
  if (strcmp "" $entcopybuf) [
    pastehilite
    reorient // temp; real fix will be in octaedit
    onrelease [ 
      if (opaquepaste) delcube 
      paste
      entpaste
      if ($cancelpaste) [ cancelsel ] 
    ] 
  ] [
    entreplace
    if ($cancelpaste) [ cancelsel ] 
  ] 
]

/////// Selection ///////////////

equaltype  = [
  if (strcmp * $arg1) [ result 1 ] [
    result (strcmp (et) $arg1)
  ]
]

equalattr  = [
  if (strcmp * $arg2) [ result 1 ] [
    result (= (ea $arg1) $arg2)
  ]
]

// select ents with given properties
// '*' is wildcard
entfind    = [ 
  if (= ($numargs) 1) [
    entselect     [ equaltype @@arg1 ]     
  ] [ if (= ($numargs) 2) [
    entselect [ && (equaltype @@arg1)         [equalattr 0 @@arg2] ]
  ] [ if (= ($numargs) 3) [
    entselect [ && (equaltype @@arg1) (&&     [equalattr 0 @@arg2] [equalattr 1 @@arg3]) ]
  ] [ if (= ($numargs) 4) [
    entselect [ && (equaltype @@arg1) (&& (&& [equalattr 0 @@arg2] [equalattr 1 @@arg3])     [equalattr 2 @@arg4]) ]
  ] [ if (= ($numargs) 5) [
    entselect [ && (equaltype @@arg1) (&& (&& [equalattr 0 @@arg2] [equalattr 1 @@arg3]) (&& [equalattr 2 @@arg4] [equalattr 3 @@arg5])) ]
  ] [
  	entselect [ && (equaltype @@arg1) (&& (&& (&& [equalattr 0 @@arg2] [equalattr 1 @@arg3]) (&& [equalattr 2 @@arg4] [equalattr 3 @@arg5])) [equalattr 4 @@arg6]) ]
  ]]]]]
]

lse        = [
  line = ""
  count = 0
  entloop [
    line  = ( concatword $line (entget) "		" )
    count = ( + $count 1 )
    if (> $count 4) [
      echo $line
      line = ""
      count = 0
    ]
  ]
  if (> $count 0 ) [ echo $line ]
  echo (enthavesel) entities selected
]

enttoggle  = [ entmoving 1; entmoving 0; ]
entaddmove = [ entmoving 2 ]

grabbing = 0

drag       = [ dragging 1; onrelease [ dragging 0 ] ]
corners    = [ selectcorners 1; dragging 1; onrelease [ selectcorners 0; dragging 0 ] ]
entadd     = [ entaddmove; entmoving 0; ]
editmove   = [ moving 1; onrelease [ moving 0 ]; result $moving ]
entdrag    = [ entaddmove; onrelease [entmoving 0]; result $entmoving ] 
editdrag   = [ cancelsel; || (entdrag) [ drag ] ]
selcorners = [ if ($hmapedit) [ hmapselect ] [ cancelsel; || (entdrag) [ corners ] ] ]
editextend = [ || entdrag [ selextend; reorient; editmove ] ]

editmovewith    = [
  if (havesel) [
    || (editmove) [ @arg1 ]
    onrelease [ moving 0; dragging 0 ]
  ] [
    @arg1
  ]
]

editmovecorner = [ editmovewith selcorners ]
editmovedrag   = [ editmovewith editdrag ]

////// Other Editing commands /////////

editfacewentpush = [ 
  
  if (|| [havesel] [! (enthavesel)] ) [
    if ($moving) [ 
      pushsel $arg1 
    ] [ 
      entcancel
      editface $arg1 $arg2
    ] 
  ] [
      if ($entmoving) [ entpush $arg1 ] [ ent_action_@(et) ]
  ]
]

entswithdirection = "playerstart teledest mapmodel creature box barrel platform elevator"

inlist = [
  r = 0
  n = ( listlen $arg2 )
  loop i $n [
    if (strcmp $arg1 (at $arg2 $i)) [
      r = 1 
    ]
  ]
  result $r
]

entdirection = [
  if ( && (enthavesel) [ = (havesel) 0 ] ) [
    if (inlist (et) $entswithdirection) [
      if (> $arg1 0) [
        entproperty 0 @@arg2
        if (> (ea 0) 360) [ entproperty 0 -360 ]          
      ] [
        entproperty 0 -@@arg2
        if (< (ea 0) 0) [ entproperty 0 360 ]
      ]
    ]
    result 1
  ] [
    result 0
  ]
]

editdel    = [ delcube; delent ]
editflip   = [ flip; entflip ]

editrotate = [ 
  || [ entdirection $arg1 15 ] [ 
      rotate $arg1
      entrotate $arg1 
    ] 
]

editcut    = [  
  hadselection = (havesel)
  moving 1
  if ($moving) [
    copy;     entcopy
    delcube;  delent
    onrelease [ 
      moving 0
      paste
      entpaste
      if ( ! $hadselection ) [ cancelsel ]
    ]
  ]
]

passthrough = [ 
  passthroughsel $arg1; 
  if ($arg1) [
    passthroughcube_bak = $passthroughcube
    passthroughcube 1
  ] [
    passthroughcube $passthroughcube_bak
  ]
  entcancel 
  if ( setting_entediting ) [ entediting ( ! $arg1 ) ]
  
]

parts = "sparkle smoke fountain fireball flare bar bar lightning fire smoke water text snow"
teams = "0neutral 3red 1blue"
creatures = "butterfly dragon golem bear rat snake wolf"

genenthud = [
	//mapmodel, elevator, platform, box, barrel, teleport
	if (> (strstr "barrel box teleport mapmodel elevator platform" (et)) -1) [
		if (&& (strcmp "teleport" (et)) (= (ea 1) 0)) [entname = default] [
			if (< (ea 1) 0) [entname = invisible] [
				if (strcmp (mapmodelname (ea 1)) "") [ entname = "3Invalid!" ] [
					entname = (mapmodelname (ea 1))
				]
			]
		]
		format "model: 0%1" $entname
	] [//needs nested if's or it won't work for anything cept the last one defined... boo
		//particles
		if (strcmp "particles" (et)) [
			if (&& (> (ea 0) -1) (< (ea 0) 12)) [
				entname = (at $parts (ea 0))
			] [
				if (&& (> (ea 0) 31) (< (ea 0) 36)) [
					entname = "lensflare"
				] [
					entname = "unknown"
				]
			]
			
			if (|| (= (ea 0) 4) (&& (< (ea 0) 11) (> (ea 0) 6))) [
				parttocol (ea 3)
				format "%1 particles; RGB: 3%2 0%3 1%4" $entname $col1 $col2 $col3
			] [
				if (|| (|| (= (ea 0) 3) (= (ea 0) 5)) (|| (= (ea 0) 6) (= (ea 0) 11))) [
					parttocol (ea 2)
					format "%1 particles; RGB: 3%2 0%3 1%4" $entname $col1 $col2 $col3
				] [
					if (&& (> (ea 0) 31) (< (ea 0) 36)) [
						format "%1 particles; RGB: 3%2 0%3 1%4" $entname (ea 1) (ea 2) (ea 3)
					] [
						format "%1 particles" $entname
					]
				]
			]
		] [
			//creature
			if (strcmp (et) "creature") [
				if (&& (> (ea 1) -1) (< (ea 1) (listlen $creatures))) [
					entname = (at $creatures (ea 1))
				] [
					entname = "3Invalid!"
				]
			format "creature: 0%1" $entname
			] [
				//actor
				if (strcmp "actor" (et)) [
					format "name: 0%1" (entgetinfo)
				] [
					//flag, playerstart
					if (|| (strcmp "flag" (et)) (strcmp "playerstart" (et))) [
						if (&& (< (ea 1) 3) (> (ea 1) -1)) [
							entname = (at $teams (ea 1))
						] [
							entname = "3Invalid!"
						]
						format "team: %1" $entname
					] [
						//banana
						if (strcmp "banana" (et)) [
							if (&& (> (ea 0) 0) (< (ea 0) 7)) [
								entname = "not implemented"
							] [
								if (= (ea 0) 0) [
									entname = "not implemented (random)"
								] [
									entname = "not implemented (3Invalid!0)"
								]
							]
							format "item: 0%1" $entname
						] [
							//light
							if (> (strstr "light dynlight" (et)) -1) [
								if (= (ea 0) 0) [
									entname = "0infinity7"
								] [
									if (< (ea 0) 0) [
										entname = "3Invalid!7"
									] [
										entname = (format "0%17" (ea 0))
									]
								]
								loop i 3 [
									if (< (ea (+ 1 $i)) 0) [
										entname = (format "%1 %2" $entname (concatword "4" (ea (+ 1 $i))))
									] [
										entname = (format "%1 %2" $entname (ea (+ 1 $i)))
									]
								]
								if (strcmp (et) "dynlight") [
									if (< (ea 4) 0) [
										format "3Invalid tag! 0must be 0 or above"
									] [
										format "radius: %1; RGB: 3%2 0%3 1%4 7tag: 0%5" (at $entname 0) (at $entname 1) (at $entname 2) (at $entname 3) (ea 4)
									]
								] [
									format "radius: %1; RGB: 3%2 0%3 1%4" (at $entname 0) (at $entname 1) (at $entname 2) (at $entname 3)
								]
							] [
								//sounds
								if (strcmp "sound" (et)) [
									if (< (ea 0) 0) [
										entname = "3Invalid!"
									] [
										if (strcmp (entsoundname (ea 0)) "") [
											entname = "3Unregistered index"
										] [
											entname = (entsoundname (ea 0))
										]
									]
									format "soundfile: 0%1" $entname
								]
							]
						]
					]
				]	
			]
		]
	]
]

edithud = [
	if $hmapedit [
		format [0gridsize: %3 worldsize: %4
brush no. %1: %2] $brushindex $brushname $gridpower (mapsize)
	] [
		if $blendpaintmode [
			format [0paintmode %1: %2
brush no. %3: %4] $blendpaintmode (at $paintmodes $blendpaintmode) (curblendbrush) (getblendbrushname (curblendbrush))
		] [
			if (enthavesel) [
				format [0%1
%2] (concatword (entget) " : " (enthavesel) " selected") (genenthud)
			] [
				if (|| (strcmp (gettexname (getseltex) 0) (gettexname 0 0)) (strcmp (getseltex) "")) [
					format [0gridsize: %1 worldsize: %2] $gridpower (mapsize)
				] [
					format [0gridsize: %1 worldsize: %3
texture slot %4: %2] $gridpower (gettexname (getseltex) 0) (mapsize) (getseltex)
				]
			]
		]
	]
]

VARoutlinestatus = 0

//default blend brushes
addblendbrush circle_16_soft   packages/brushes/circle_16_soft.png
addblendbrush circle_16_hard   packages/brushes/circle_16_hard.png
addblendbrush circle_16_solid  packages/brushes/circle_16_solid.png
addblendbrush circle_32_soft   packages/brushes/circle_32_soft.png
addblendbrush circle_32_hard   packages/brushes/circle_32_hard.png
addblendbrush circle_32_solid  packages/brushes/circle_32_solid.png
addblendbrush circle_64_soft   packages/brushes/circle_64_soft.png
addblendbrush circle_64_hard   packages/brushes/circle_64_hard.png
addblendbrush circle_64_solid  packages/brushes/circle_64_solid.png
addblendbrush circle_128_soft  packages/brushes/circle_128_soft.png
addblendbrush circle_128_hard  packages/brushes/circle_128_hard.png
addblendbrush circle_128_solid packages/brushes/circle_128_solid.png

addblendbrush square_16_hard  packages/brushes/square_16_hard.png
addblendbrush square_16_solid packages/brushes/square_16_solid.png
addblendbrush square_32_hard  packages/brushes/square_32_hard.png
addblendbrush square_32_solid packages/brushes/square_32_solid.png
addblendbrush square_64_hard  packages/brushes/square_64_hard.png
addblendbrush square_64_solid packages/brushes/square_64_solid.png

addblendbrush noise_64  packages/brushes/noise_64.png
addblendbrush noise_128 packages/brushes/noise_128.png

//extra shapes and um... stuff
addblendbrush star_32_hard  packages/brushes/star_32_hard.png
addblendbrush star_32_solid packages/brushes/star_32_solid.png
addblendbrush star_64_hard  packages/brushes/star_64_hard.png
addblendbrush star_64_solid packages/brushes/star_64_solid.png

addblendbrush triangle_32_hard  packages/brushes/triangle_32_hard.png
addblendbrush triangle_32_solid packages/brushes/triangle_32_solid.png
addblendbrush triangle_64_hard  packages/brushes/triangle_64_hard.png
addblendbrush triangle_64_solid packages/brushes/triangle_64_solid.png
